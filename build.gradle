version = '1.0'

apply plugin: 'java'
apply plugin: 'maven'
sourceCompatibility = 1.7

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId 'org.ianp'
        artifactId 'praxis-java'
        inceptionYear '2013'
        packaging 'jar'
        licenses {
            license {
                name 'Eclipse Public License (Version 1.0)'
                url 'http://www.eclipse.org/legal/epl-v10.html'
                distribution 'repo'
            }
        }
    }
}

repositories {
    mavenRepo url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:14.0-rc1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gradle.taskGraph.whenReady { taskGraph ->
    if (!taskGraph.hasTask(':release')) {
        version = version + '-SNAPSHOT'
    }
}

